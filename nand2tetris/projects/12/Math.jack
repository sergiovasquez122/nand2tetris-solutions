// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

  static int powers_of_two;

   /** Initializes the library. */
   function void init() {
       let powers_of_two = Array.new(16);
       let powers_of_two[0] = 1;
       let powers_of_two[1] = 2;
       let powers_of_two[2] = 4;
       let powers_of_two[3] = 8;
       let powers_of_two[4] = 16;
       let powers_of_two[5] = 32;
       let powers_of_two[6] = 64;
       let powers_of_two[7] = 128;
       let powers_of_two[8] = 256;
       let powers_of_two[9] = 512;
       let powers_of_two[10] = 1024;
       let powers_of_two[11] = 2048;
       let powers_of_two[12] = 4096;
       let powers_of_two[13] = 8192;
       let powers_of_two[14] = 16384;
       let powers_of_two[15] = 16384+16384;
       return;
   }

    function boolean bit(int x, int i){
      return ~((x & powers_of_two[i]) = 0);
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
      if(x < 0){
        return -x;
      } else {
        return x;
      }
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
      var int i;
      var int sum;
      var int shifted_x;
      var boolean ith_bit_of_y;
      let i = 0;
      let sum = 0;
      let shifted_x = x;
      while(i < 16){
        let ith_bit_of_y = Math.bit(y, i);
        if(ith_bit_of_y){
          let sum = sum + shifted_x;
        }
        let shifted_x = shifted_x + shifted_x;
        let i = i + 1;
      }
      return sum;
    }

    function int divide_helper(int x, int y){
      var int q;
      var int doubleQ;
      if(y > x){
        return 0;
      }
      let q = Math.divide_helper(x, y + y);
      let doubleQ = q + q;
      if((x - (doubleQ * y)) < y){
        return doubleQ;
      } else {
        return doubleQ + 1;
      }
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
      var boolean x_negative;
      var boolean y_negative;
      var int x_abs;
      var int y_abs;
      let x_negative = x < 0;
      let y_negative = y < 0;
      let x_abs = Math.abs(x);
      let y_abs = Math.abs(y);
      if(x_negative & y_negative){
        return Math.divide_helper(x_abs, y_abs);
      }
      if((~x_negative) & (~y_negative)){
        return Math.divide_helper(x_abs, y_abs);
      }
      return -Math.divide_helper(x_abs, y_abs);
    }


    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
      var int lo;
      var int hi;
      var int mid;
      var int sqr;
      let lo = 0;
      let hi = x;
      while(~(lo > hi)){
        let mid = lo + ((hi - lo) / 2);
        let sqr = mid * mid;
        if(sqr < x) {
          let lo = mid + 1;
        }
        if(sqr > x){
          let hi = mid - 1;
        }
        if(sqr = x){
          return mid;
        }
      }
      return lo;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
      if(a > b){
        return a;
      } else {
        return b;
      }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
      if(a < b){
        return a;
      } else {
        return b;
      }
    }
}
